buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.11.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion project.ANDROID_BUILD_SDK_VERSION as int
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    // customize apk output names
    // For each application variant (release, debug, etc.)
    applicationVariants.all { variant ->
        def outputs = variant.getOutputs()
        outputs.each { output ->
            File originalFile = output.outputFile
            String date = (new Date()).format('yyyy-MM-dd')
            String newFilename = date + '_' + originalFile.getName()
            output.outputFile = new File(originalFile.parent, newFilename)
        }
    }

    defaultConfig {
        minSdkVersion project.ANDROID_BUILD_MIN_SDK_VERSION as int
        targetSdkVersion project.ANDROID_BUILD_TARGET_SDK_VERSION as int
        versionCode 2
        versionName "Pre-Alpha 2 (v0.2.5)"

        File secureFile = rootProject.file('secure.properties')
        Properties secureProp = new Properties()
        secureProp.load(new FileInputStream(secureFile))

        manifestPlaceholders = [ googleGameServicesKey:secureProp['google_game_api']]
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('../keystore/debug.keystore')
        }

        release {
            storeFile rootProject.file('../keystore/release.keystore')

            File propertiesFile = rootProject.file('../keystore/keystore.properties')
            Properties properties = new Properties();
            properties.load(new FileInputStream(propertiesFile));

            storePassword properties['storePassword']
            keyAlias properties['keyAlias']
            keyPassword properties['keyPassword']
        }
    }

    buildTypes {
        debug {
            jniDebugBuild true
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            jniDebugBuild false
            debuggable false
            signingConfig signingConfigs.release
            ext.betaDistributionEmailsFilePath = 'beta_distribution_emails.txt'
            ext.betaDistributionReleaseNotesFilePath = 'release_notes.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':BaseGameUtils')
    compile project(':GameMechanic')
    compile project(':ToolBox')

    compile 'com.crashlytics.android:crashlytics:1.1.11'
}
