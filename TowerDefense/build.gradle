buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.14.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion project.ANDROID_BUILD_SDK_VERSION as int
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    // customize apk output names
    // For each application variant (release, debug, etc.)
    applicationVariants.all { variant ->
        def outputs = variant.getOutputs()
        outputs.each { output ->
            File originalFile = output.outputFile
            String date = (new Date()).format('yyyy-MM-dd')
            String newFilename = date + '_' + originalFile.getName()
            output.outputFile = new File(originalFile.parent, newFilename)
        }
    }

    defaultConfig {
        minSdkVersion project.ANDROID_BUILD_MIN_SDK_VERSION as int
        targetSdkVersion project.ANDROID_BUILD_TARGET_SDK_VERSION as int
        versionCode 1
        versionName "Pre-Alpha 2 (v0.2.6)"

        File secureFile = rootProject.file('secure.properties')
        Properties secureProp = new Properties()
        secureProp.load(new FileInputStream(secureFile))

        manifestPlaceholders = [google_game_api: secureProp['google_game_api']]
        manifestPlaceholders = [crashlytics_api: secureProp['crashlytics_api']]
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('../keystore/debug.keystore')
        }

        release {
            storeFile rootProject.file('../keystore/release.keystore')

            File propertiesFile = rootProject.file('../keystore/keystore.properties')
            Properties properties = new Properties();
            properties.load(new FileInputStream(propertiesFile));

            storePassword properties['storePassword']
            keyAlias properties['keyAlias']
            keyPassword properties['keyPassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'

            jniDebuggable true
            debuggable true
            signingConfig signingConfigs.debug

            ext.betaDistributionEmailsFilePath = 'distribution_emails_debug.txt'
            ext.betaDistributionReleaseNotesFilePath = 'release_notes.txt'
        }
        release {
            jniDebuggable false
            debuggable false
            signingConfig signingConfigs.release
            
            ext.betaDistributionEmailsFilePath = 'distribution_emails_release.txt'
            ext.betaDistributionReleaseNotesFilePath = 'release_notes.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':BaseGameUtils')
    compile project(':GameMechanic')
    compile project(':ToolBox')

    compile 'com.google.android.gms:play-services:6.5.87'

    compile('com.crashlytics.sdk.android:crashlytics:2.2.0@aar') {
        transitive = true;
    }
}
